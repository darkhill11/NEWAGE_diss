!============================================================================!
! TSTAGG.TAB: Time Series Trade Data Aggregation TAB file                    !
! GTAP Data Base Version 8 (2011)                                            !
! This file reads the time series trade data and sets files to generate      !
!       an aggregated time series trade data base.                           !
!============================================================================!

Equation (none);

!============================================================================!
! Input files                                                                !
!============================================================================!

File        TSTDAT   # Disaggregate TS trade data for ag com     #;
File        DSETS     # Set specification for disaggregate data   #;
File        ASETS     # Set specification for aggregate data      #;

!============================================================================!
! Output files                                                               !
!============================================================================!

File (new)      ATSTDAT     # Aggregated time series trade data        #;

!============================================================================!
! Sets for dis-aggregate data                                                !
!============================================================================!

Set DREG       # REGIONS #
           maximum size 45 read elements from file TSTDAT header "REG";

Set DTRAD_COMM # TRADED COMMODITIES #
           maximum size 50 read elements from file DSETS header "H2";

Set DMERC_COMM # MERCHANDIZE TRADE #
           maximum size 50 read elements from file TSTDAT header "TSCO";

Subset DMERC_COMM is subset of DTRAD_COMM;

Set YEAR # periods #
 maximum size  40 read elements from file TSTDAT header "YEAR";

!============================================================================!
! Sets for aggregate data                                                    !
!============================================================================!

Set REG       # REGIONS IN AGGREGATE DATA SET #
           maximum size 45 read elements from file ASETS header "H1";

Set TRAD_COMM # TRADED COMMODITIES #
           maximum size 50 read elements from file ASETS header "H2";

! ===========================================================================!
!                    READING AGGREGATION SCHEME                              !
! ===========================================================================!

Mapping COM from DTRAD_COMM to TRAD_COMM;
         Read (BY_ELEMENTS) COM from file ASETS header "DCOM";

Mapping REGD from DREG to REG;
         Read (BY_ELEMENTS) REGD from file ASETS header "DREG";

!Define the set of aggregated commmodities with merchandize trade on the fly:!
!The question here is: Does trad_comm's element "i" contain at least one
element in dmerc_comm? To answer this, we first create a unit vector of
dimension dtrad_comm:!

Coefficient (all,c,DTRAD_COMM) UNITVEC(c) #Auxiliary unit vector#;
Formula (all,c,DTRAD_COMM) UNITVEC(c)=1;

!then, the answer is found by aggregating UNITVEC from dtrad_comm to trad_comm  
including only those elements in dmerc_commm. The idea is that those trad_comm
that do not contain any dmerc_comm will have a value of zero.
In GEMPACK notation:!

Coefficient (all,i,TRAD_COMM) ISAMER(i) #Is a Merchandise Commodity?#;
Formula (all,i,TRAD_COMM)
ISAMER(i)= sum{c, DMERC_COMM: COM(c) eq i, UNITVEC(c) };

!Thus, If ISAMER(i) > 0, the answer is yes:!

Set MERC_COMM = (all, i, TRAD_COMM : ISAMER(i)>0);

!The last step is to define a mapping from dmerc_comm to merc_comm based on
the mapping from dtrad_comm to atrad_comm!

Mapping D2AMERC from  DMERC_COMM to MERC_COMM;
Formula (all,c,DMERC_COMM) D2AMERC(c)=COM(c);

!<End of Dynamic Aggregated Merchandise Commodities Set Definition!
!!
!============================================================================!
! Read values from dis-aggregate database.                                   !
!============================================================================!

Coefficient
 (all,i,DMERC_COMM)(all,r,DREG)(all,s,DREG)(all,t,YEAR)
VTTS(i,r,s,t);
Read VTTS from file TSTDAT header "VTTS";

Coefficient (all,c,MERC_COMM)(all,t,REG)(all,n,REG)(all,u,YEAR)
CVTTS(c,t,n,u);

Formula (all,c,MERC_COMM)(all,t,REG)(all,n,REG)(all,u,YEAR)
CVTTS(c,t,n,u)=0.0;

Formula (all,c,MERC_COMM)(all,t,REG)(all,n,REG)(all,u,YEAR)
     CVTTS(c,t,n,u) = CVTTS(c,t,n,u) +
     sum{i,DMERC_COMM: COM(i) eq c,
     sum{r,DREG:       REGD(r) eq t,
     sum{s,DREG:       REGD(s) eq n,  VTTS(i,r,s,u)}}};

!============================================================================!
!                     WRITING OF TS TRADE DATABASE                           !
!============================================================================!

Write CVTTS to file ATSTDAT header  "VTTS"
   longname "Time Series Trade Data";

!=============================== end of file ================================!
