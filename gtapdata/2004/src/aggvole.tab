!============================================================================!
! AGGVOLE.TAB: Energy Volume Data Aggregation TAB File                       !
! GTAP Data Base Version 8 (2011)                                            !
! This file reads the global energy volume data.                             !
! Aggregates Energy Commodities as well                                      !
!============================================================================!

Equation (none);

!============================================================================!
! Input files                                                                !
!============================================================================!

File DDATA #Disaggregate data in GTAP notation#;
File DSETS #Set specification for disaggregate data#;
File ASETS #Set specification for aggregate data#;

!============================================================================!
! Output files                                                               !
!============================================================================!

File (new) EGYVOL # Aggregated energy volume data #;

!============================================================================!
! Sets for dis-aggregate data                                                !
!============================================================================!

Set DREG #Regions#
    read elements from file DSETS header "H1";

Set DTRAD_COMM #Traded commodities#
    read elements from file DSETS header "H2";

Set CGDS_COMM # capital goods commodities # (CGDS);

Set DPROD_COMM # produced commodities # = DTRAD_COMM union CGDS_COMM;

Set DEGY_COMM #Energy commodities#
    read elements from file DDATA header "EC";
Subset DEGY_COMM is subset of DTRAD_COMM ;

!============================================================================!
! Sets for aggregate data                                                    !
!============================================================================!

Set REG #Regions in aggregate data set#
    read elements from file ASETS header "H1";

Set TRAD_COMM #Traded commodities#
    read elements from file ASETS header "H2";

Set PROD_COMM # produced commodities # = TRAD_COMM union CGDS_COMM;

! ===========================================================================!
!                    READING AGGREGATION SCHEME                              !
! ===========================================================================!

Mapping COM from DTRAD_COMM to TRAD_COMM;
         Read (BY_ELEMENTS) COM from file ASETS header "DCOM";

Mapping REGD from DREG to REG;
         Read (BY_ELEMENTS) REGD from file ASETS header "DREG";

!Define the set of aggregated energy commmodities on the fly:!
!The question here is: Does atrad_comm's element "i" contain at least one
element in degy_comm? To answer this, we first create a unit vector of
dimension dtrad_comm:!

COEFFICIENT (all,c,DTRAD_COMM) UNITVEC(c) #Auxiliary unit vector#;
FORMULA (all,c,DTRAD_COMM) UNITVEC(c)=1;

!then, the answer is found by aggregating UNITVEC from dtrad_comm to atrad_comm
including only those elements in degy_commm. The idea is that those atrad_comm
that do not contain any degy_comm will have a value of zero.
In GEMPACK notation:!

COEFFICIENT (all,i,TRAD_COMM) ISAEGY(i) #Is an Aggregated Energy Commodity?#;
FORMULA (all,i,TRAD_COMM)
ISAEGY(i)= sum{c, DEGY_COMM: COM(c) eq i, UNITVEC(c) };

!Thus, If ISAEGY(i) > 0, the answer is yes:!

SET EGY_COMM = (all, i, TRAD_COMM : ISAEGY(i)>0);

!The last step is to define a mapping from degy_comm to aegy_comm based on
the mapping from dtrad_comm to atrad_comm!

MAPPING D2AEGY from  DEGY_COMM to EGY_COMM;
FORMULA (all,c,DEGY_COMM) D2AEGY(c)=COM(c);

!============================================================================!
! Read values from dis-aggregate database.                                   !
!============================================================================!

Coefficient (all,i,DEGY_COMM)(all,j,DPROD_COMM)(all,r,DREG)
    EDF(i,j,r)
    #Volume of purchases of domestic energy i by firms in j in region r #;

Coefficient (all,i,DEGY_COMM)(all,j,DPROD_COMM)(all,r,DREG)
    EIF(i,j,r)
    #Volume of purchases of imported energy i by firms in j in region r #;

Coefficient (all,i,DEGY_COMM)(all,r,DREG)
    EDP(i,r)
    #Volume of household purchases of domestic i in region r#;

Coefficient (all,i,DEGY_COMM)(all,r,DREG)
    EIP(i,r)
    #Volume of household purchases of imported i in region r#;

Coefficient (all,i,DEGY_COMM)(all,r,DREG)
    EDG(i,r)
    #Volume of government purchases of domestic i in region r#;

Coefficient (all,i,DEGY_COMM)(all,r,DREG)
    EIG(i,r)
    #Volume of government purchases of imported i in region r#;

Coefficient (all,i,DEGY_COMM)(all,r,DREG)(all,s,DREG)
    EXI(i,r,s) #Volume of bilateral trade#;

!----------------------------------------------------------------------------!
!                   Reading disaggregated data                               !
!----------------------------------------------------------------------------!

Read EDF from file DDATA header "EDF";
Read EIF from file DDATA header "EIF";

Read EDP from file DDATA header "EDP";
Read EIP from file DDATA header "EIP";

Read EDG from file DDATA header "EDG";
Read EIG from file DDATA header "EIG";

Read EXI from file DDATA header "EXI";

!============================================================================!
! AGGREGATION of coefficients                                                !
!============================================================================!

Coefficient (all,i,EGY_COMM)(all,j,PROD_COMM)(all,r,REG)        CEDF(i,j,r);
Formula (all,i,EGY_COMM)(all,j,TRAD_COMM)(all,r,REG)
    CEDF(i,j,r) = sum{l,DEGY_COMM: D2AEGY(l) eq i,
                   sum{k,DTRAD_COMM: COM(k) eq j,
                   sum{s,DREG: REGD(s)  eq r, EDF(l,k,s) }}};
Formula (all,i,EGY_COMM)(all,j,CGDS_COMM)(all,r,REG)
    CEDF(i,j,r) = sum{l,DEGY_COMM: D2AEGY(l) eq i,
                   sum{s,DREG: REGD(s)  eq r, EDF(l,j,s) }};
Write
    CEDF to file EGYVOL header "EDF"
    longname "volume of domestic purchases by firms (Mtoe)";


Coefficient (all,i,EGY_COMM)(all,j,PROD_COMM)(all,r,REG)        CEIF(i,j,r);
Formula (all,i,EGY_COMM)(all,j,TRAD_COMM)(all,r,REG)
    CEIF(i,j,r) = sum{l,DEGY_COMM: D2AEGY(l) eq i,
                   sum{k,DTRAD_COMM: COM(k) eq j,
                   sum{s,DREG: REGD(s)  eq r, EIF(l,k,s) }}};
Formula (all,i,EGY_COMM)(all,j,CGDS_COMM)(all,r,REG)
    CEIF(i,j,r) = sum{l,DEGY_COMM: D2AEGY(l) eq i,
                   sum{s,DREG: REGD(s)  eq r, EIF(l,j,s) }};
Write
    CEIF to file EGYVOL header "EIF"
    longname "volume of import purchases by firms (Mtoe)";


Coefficient (all,i,EGY_COMM)(all,r,REG)                         CEDP(i,r);
Formula (all,i,EGY_COMM)(all,r,REG)
    CEDP(i,r) = sum{l,DEGY_COMM: D2AEGY(l) eq i,
                 sum{s,DREG: REGD(s) eq r, EDP(l,s) }};
Write
    CEDP to file EGYVOL header "EDP"
    longname "volume of domestic purchases by households (Mtoe)";


Coefficient (all,i,EGY_COMM)(all,r,REG)                         CEIP(i,r);
Formula (all,i,EGY_COMM)(all,r,REG)
    CEIP(i,r) = sum{l,DEGY_COMM: D2AEGY(l) eq i,
                 sum{s,DREG: REGD(s) eq r, EIP(l,s) }};
Write
    CEIP to file EGYVOL header "EIP"
    longname "volume of import purchases by households (Mtoe)";


Coefficient (all,i,EGY_COMM)(all,r,REG)                         CEDG(i,r);
Formula (all,i,EGY_COMM)(all,r,REG)
    CEDG(i,r) = sum{l,DEGY_COMM: D2AEGY(l) eq i,
                 sum{s,DREG: REGD(s) eq r, EDG(l,s) }};
Write
    CEDG to file EGYVOL header "EDG"
    longname "volume of domestic purchases by government (Mtoe)";


Coefficient (all,i,EGY_COMM)(all,r,REG)                         CEIG(i,r);
Formula (all,i,EGY_COMM)(all,r,REG)
    CEIG(i,r) = sum{l,DEGY_COMM: D2AEGY(l) eq i,
                 sum{s,DREG: REGD(s) eq r, EIG(l,s) }};
Write
    CEIG to file EGYVOL header "EIG"
    longname "volume of import purchases by government (Mtoe)";


Coefficient (all,i,EGY_COMM)(all,r,REG)(all,s,REG)              CEXI(i,r,s);
Formula (all,i,EGY_COMM)(all,r,REG)(all,s,REG)
    CEXI(i,r,s) = sum{l,DEGY_COMM: D2AEGY(l) eq i,
                  sum{u,DREG: REGD(u) eq r,
                  sum{v,DREG: REGD(v) eq s, EXI(l,u,v) }}};
Write
    CEXI to file EGYVOL header "EXI"
    longname "volume of bilateral trade (Mtoe)";

!======================= END OF FILE AGGVOLE.TAB ============================!
